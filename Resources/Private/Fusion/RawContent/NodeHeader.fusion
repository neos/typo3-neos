prototype(Neos.Neos:RawContent.NodeHeader) < prototype(Neos.Fusion:Component) {

  labelParts = ${String.split(node.nodeType.label, ':')}
  icon = ${node.nodeType.fullConfiguration.ui.icon}
  visibilityInformations = Neos.Fusion:DataStructure {
    hidden = 'hidden'
    hidden.@if.isHidden = ${node.hidden}
    hiddenBeforeDateTime = ${'hidden before ' + Date.format(node.hiddenBeforeDateTime, 'Y-m-d H:i:s')}
    hiddenBeforeDateTime.@if.hasHiddenBeforeDateTime = ${node.hiddenBeforeDateTime}
    hiddenAfterDateTime = ${'hidden after ' + Date.format(node.hiddenAfterDateTime, 'Y-m-d H:i:s')}
    hiddenAfterDateTime.@if.hasHiddenAfterDateTime = ${node.hiddenAfterDateTime}
    @process.join = ${Array.join(value, ', ')}
  }

  renderer = afx`
    <header>
      <i class={'fas fa-' + props.icon + ' ' + props.icon} @if={props.icon}></i>&nbsp;
      {(node.tethered || q(node).is('[instanceof Neos.Neos:Document]')) ? node.label : Translation.translate(props.labelParts[2], node.nodeType.label, [], props.labelParts[1], props.labelParts[0])}
      <span @if={props.visibilityInformations}> - {props.visibilityInformations}</span>
    </header>
  `
}
